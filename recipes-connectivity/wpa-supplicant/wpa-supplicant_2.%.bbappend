FILESEXTRAPATHS:prepend := "${THISDIR}/files:"

SRC_URI =+ " \
	file://dbus-wpa_supplicant.conf;subdir=nilrt \
	file://fi.epitest.hostap.WPASupplicant.service;subdir=nilrt \
	file://fi.w1.wpa_supplicant1.service;subdir=nilrt \
	file://report-eap-authentication-state.patch \
	file://wep-hexkeys-and-wpa-psk-hexkeys-fix.patch \
	file://wpa_supplicant_conf;subdir=nilrt \
"

PACKAGECONFIG:append = " openssl"


do_install:append () {
	install -m 644 ${WORKDIR}/nilrt/dbus-wpa_supplicant.conf ${D}/${sysconfdir}/dbus-1/system.d
	install -m 644 ${WORKDIR}/nilrt/*.service ${D}/${datadir}/dbus-1/system-services

	# Remove the wpa_supplicant.conf file installed by the OE recipe. This will be replaced by
	# a version generated by wpa_supplicant_conf in /etc/natinst/share.
	rm -f ${D}/${sysconfdir}/wpa_supplicant.conf

	install -m 755 ${WORKDIR}/nilrt/wpa_supplicant_conf ${D}/${sysconfdir}/network
}
