SUMMARY = "NI gRPC Device Server"
DESCRIPTION = "gRPC server providing remote access to NI device driver APIs."
HOMEPAGE = "https://github.com/ni/grpc-device"
SECTION = "base"
LICENSE = "MIT & Apache-2.0"
LIC_FILES_CHKSUM = "\
	file://LICENSE;md5=f7f2c0314387ea903bd40965abfad353 \
	file://ThirdPartyNotices.txt;md5=57283cec49f9773bb10bac48da2e56df \
"


DEPENDS += "\
	grpc-native \
	googletest-native \
	nlohmann-json-native \
	protobuf-native \
	python3-grpcio-tools-native \
	python3-mako-native \
	python3-native \
	utf8cpp-native \
	python3-schema-native \
"

PV = "2.4.0"


SRC_URI = "\
	git://github.com/ni/grpc-device.git;name=grpc-device;branch=main;protocol=https \
	file://ptest \
	file://0001-Subject-PATCH-CMakeLists.txt-remove-local-protobuf-i.patch \
	file://0002-CMakeLists-fixup-utf8cpp-library-link.patch \
	file://0003-CMakeLists-use-native-python3-binaries.patch \
	file://0001-CMakeLists-find-gpr-library.patch \
"
SRCREV_grpc-device = "aeef9995eb634ed5dc4d87dc5adbfd7c66d9fa64"
SRCREV_FORMAT = "grpc-device"

S = "${WORKDIR}/git"


inherit cmake python3native

EXTRA_OECMAKE += "-DCMAKE_CROSSCOMPILING=True -DCMAKE_BUILD_TYPE=Release"
OECMAKE_TARGET_COMPILE = "ni_grpc_device_server"
OECMAKE_GENERATOR = "Unix Makefiles"


#inherit ptest
#
#BUILD_PTEST = "${B}/ptest"
#RDEPENDS:${PN}-ptest += "\
#	${PN} \
#	bash \
#	python3-grpcio \
#"
#
#do_compile_ptest:append () {
#	install -d ${BUILD_PTEST}
#	python3 -m grpc_tools.protoc \
#		-I${S}/source/protobuf \
#		--python_out=${BUILD_PTEST} \
#		--grpc_python_out=${BUILD_PTEST} \
#		${S}/source/protobuf/session.proto
#}
#
#do_install_ptest:append () {
#	install -d ${D}${PTEST_PATH}
#	install -m 0755 ${WORKDIR}/ptest/run-ptest ${D}${PTEST_PATH}/
#
#	install -m 0644 ${S}/examples/session/enumerate-device.py ${D}${PTEST_PATH}/
#
#	# These files are generated by the do_compile_ptest task.
#	install -m 0644 ${BUILD_PTEST}/session_pb2_grpc.py ${D}${PTEST_PATH}/
#	install -m 0644 ${BUILD_PTEST}/session_pb2.py ${D}${PTEST_PATH}/
#}


serverdir = "${D}${libdir}/${BPN}"
# grpc-device does not provide an 'install' target (yet); so overwrite the whole
# of do_install, to keep cmake from building anything further.
do_install () {
	# install server components
	install -d ${serverdir}
	install --mode=0755 ${B}/ni_grpc_device_server    ${serverdir}
	install --mode=0644 ${B}/server_config.json       ${serverdir}/server_config.json.example
	install --mode=0644 ${B}/server_capabilities.json ${serverdir}

	# package .proto files for use by developers
	install -d ${D}${datadir}/${BPN}
	install --mode=0644 ${S}/generated/**/*.proto     ${D}${datadir}/${BPN}
	install --mode=0644 ${S}/imports/protobuf/*.proto ${D}${datadir}/${BPN}

	# install example files
	install -d ${D}${datadir}/${BPN}
	cp -r ${S}/examples ${D}${datadir}/${BPN}
}


PACKAGE_BEFORE_PN = "${PN}-examples"

# ${PN}
RDEPENDS:${PN} += "\
	grpc \
	protobuf \
"

# ${PN}-dev
FILES:${PN}-dev:append = "${datadir}/${BPN}/*.proto"
RDEPENDS:${PN}-dev:append = "\
	grpc-dev \
"

# ${PN}-examples
FILES:${PN}-examples = "${datadir}/${BPN}/examples"
RDEPENDS:${PN}-examples = "\
	python3 \
"
