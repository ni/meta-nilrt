#@TYPE: Machine
#@Name: Xilinx Zynq Platform
#@DESCRIPTION: Machine configuration for Xilinx Zynq-7000

TARGET_ARCH = "arm"
#tune for the ARM Cortex A9
DEFAULTTUNE ?= "cortexa9hf-vfpv3"
require conf/machine/include/tune-cortexa9.inc

MACHINE_ARCH = "${@[d.getVar('TUNE_PKGARCH', True), d.getVar('MACHINE', True)][bool(d.getVar('MACHINE', True))]}"

MACHINE_FEATURES = "apm ext2 vfat ethernet screen usbgadget usbhost wifi"

SERIAL_CONSOLES = "115200;ttyS0"

INITRAMFS_FSTYPES = "cpio.gz.u-boot"

# we're not building a full u-boot in OE, justfw_{print|set}env and mkimage,
# thus we use a generic machine.
UBOOT_MACHINE ?= "config"

UBOOT_LOADADDRESS ?= "0x8000"
UBOOT_ENTRYPOINT ?= "${UBOOT_LOADADDRESS}"

KERNEL_EXTRA_ARGS += "LOADADDR=0x8000"

KERNEL_IMAGETYPE ?= "uImage"

NILRT_ARM_DEVICE_CODES = "\
    762F 76D3 76D6 76F2 7740 7741 7742 7743 7744 774C \
    774E 775E 77AC 77B1 77B2 77D4 77D5 77D6 7885 \
"

KERNEL_DEVICETREE = "${@' '.join(["ni-%s.dtb" % x for x in "${NILRT_ARM_DEVICE_CODES}".split()])}"

# 793C is special, we only need to build its kernel & device tree, migration &
# other features are not supported (yet)
KERNEL_DEVICETREE += "ni-793C.dtb"
